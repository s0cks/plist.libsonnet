{
  local NEWLINE = [ '' ],

  local IndentBy(amt, c = ' ') =
    std.repeat(c, amt * 2),

  local Tag(t, indent) =
    "%(indent)s<%(tag)s/>" % { tag: t, indent: IndentBy(indent) },

  local OpenTag(t, indent) =
    "%(indent)s<%(tag)s>" % { tag: t, indent: IndentBy(indent), },

  local CloseTag(t, indent) =
    "%(indent)s</%(tag)s>" % { tag: t, indent: IndentBy(indent), },

  local SingleWrap(tag, value, indent) =
    IndentBy(indent) + OpenTag(tag, 0) + value + CloseTag(tag, 0),

  SingleWrapString(value, indent):
    SingleWrap('string', value, indent),

  local MultiWrap(tag, values, indent, newline_after = true) = 
    [ OpenTag(tag, indent) ] +
    values +
    [ CloseTag(tag, indent) ] +
    (if newline_after then NEWLINE else []),

  Comment(lines):
      [ "<!--" ] +
      lines +
      [ "-->" ],

  Dict(props, indent = 1, newline_after = true):
    MultiWrap('dict', props, indent, newline_after),

  Array(values, indent = 1, newline_after = true):
    MultiWrap('array', values, indent, newline_after),

  Key(key, indent = 1):
    SingleWrap('key', key, indent),

  String(key, value, indent = 2, newline_after = true):
    [
      $.Key(key, indent),
      $.SingleWrapString(value, indent),
    ] +
    (if newline_after then NEWLINE else []),

  Bool(key, value, indent = 2, newline_after = true):
    [
      $.Key(key, indent),
      Tag(value, indent),
    ] +
    (if newline_after then NEWLINE else []),

  RunAtLoad(value = true, indent = 2, newline_after = true):
    $.Bool('RunAtLoad', value, indent, newline_after),

  KeepAlive(value = true, indent = 2, newline_after = true):
    $.Bool('KeepAlive', value, indent, newline_after),

  WorkingDirectory(value, indent = 2, newline_after = true):
    $.String('WorkingDirectory', value, indent, newline_after),

  StandardOutPath(value, indent = 2, newline_after = true):
    $.String('StandardOutPath', value, indent, newline_after),

  StandardErrorPath(value, indent = 2, newline_after = true):
    $.String('StandardErrorPath', value, indent, newline_after),

  Label(value, indent = 2, newline_after = true):
    $.String('Label', value, indent, newline_after),

  EnvironmentVariables(env, indent = 2, newline_after = true):
    [
      $.Key("EnvironmentVariables", indent),
      $.Dict(
        std.flattenDeepArray([
          [ $.Key(k, indent + 1), env[k] ]
          for k in std.objectFields(env)
        ]),
        indent,
        newline_after
      ),
    ],

  ProgramArguments(prog, args = [], indent = 2, newline_after = true):
    [
      $.Key("ProgramArguments", indent),
      $.Array(
        [ $.SingleWrapString(prog, indent + 1) ] + args,
        indent,
        newline_after),
    ],

  local PlistHeader = 
    [
      '<?xml version="1.0" encoding="UTF-8"?>',
      '<!DOCTYPE plist PUBLIC "-//Apple//DTD PLIST 1.0//EN" "http://www.apple.com/DTDs/PropertyList-1.0.dtd">',
    ],

  Plist(props):
    PlistHeader +
    [ "<plist version=\"1.0\">" ] +
    $.Dict(props, 1, false) +
    [ "</plist>" ],

  manifest(props):
    std.lines(std.flattenDeepArray(
      $.Comment([
        "*** Do not edit ***",
        "This file is auto-generated by Jsonnet",
      ]) +
      $.Plist(props)
    ))
}
